(set-info :smt-lib-version 2.6)
;;; Processed by pysmt to remove constant-real bitvector literals
(set-logic QF_FP)
(set-info :source |SPARK inspired floating point problems by Florian Schanda|)
(set-info :category "crafted")
(set-info :status sat)
(define-fun is_finite ((f Float32)) Bool (or (fp.isZero f) (fp.isNormal f) (fp.isSubnormal f)))
(define-fun two () Float32 ((_ to_fp 8 24) #x40000000))
(define-fun bound () Float32 ((_ to_fp 8 24) #b00000000100000000000000000000000))
(declare-fun a () Float32)
(declare-fun b () Float32)
(assert (is_finite a))
(assert (is_finite b))
(assert (and (fp.leq bound a) (fp.leq bound b)))
(define-fun avg_1 () Float32 (fp.add RNE (fp.div RNE a two) (fp.div RNE b two)))
(define-fun avg_2 () Float32 ((_ to_fp 8 24) RNE (fp.div RNE (fp.add RNE ((_ to_fp 11 53) RNE a) ((_ to_fp 11 53) RNE b)) ((_ to_fp 11 53) RNE two))))
(assert (not (fp.eq avg_1 avg_2)))
(check-sat)
(exit)
