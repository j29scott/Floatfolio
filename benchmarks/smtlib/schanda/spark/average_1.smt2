(set-info :smt-lib-version 2.6)
;;; Processed by pysmt to remove constant-real bitvector literals
(set-logic QF_FP)
(set-info :source |SPARK inspired floating point problems by Florian Schanda|)
(set-info :category "crafted")
(set-info :status unsat)
(define-fun t__first () Float32 ((_ to_fp 8 24) #xc1200000))
(define-fun t__last () Float32 ((_ to_fp 8 24) #x41200000))
(define-fun f2 () Float32 ((_ to_fp 8 24) #x40000000))
(define-fun in_t_range ((f Float32)) Bool (and (fp.leq t__first f) (fp.leq f t__last)))
(declare-fun a () Float32)
(declare-fun b () Float32)
(assert (in_t_range a))
(assert (in_t_range b))
(assert (fp.lt a b))
(define-fun mid () Float32 (fp.div RNE (fp.add RNE a b) f2))
(assert (not (and (fp.leq a mid) (fp.leq mid b))))
(check-sat)
(exit)
