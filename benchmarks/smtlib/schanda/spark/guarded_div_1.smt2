(set-info :smt-lib-version 2.6)
;;; Processed by pysmt to remove constant-real bitvector literals
(set-logic QF_FP)
(set-info :source |SPARK inspired floating point problems by Florian Schanda|)
(set-info :category "crafted")
(set-info :status sat)
(define-fun isFinite ((f Float32)) Bool (or (fp.isNormal f) (fp.isSubnormal f) (fp.isZero f)))
(define-fun f0 () Float32 (_ +zero 8 24))
(define-fun f0_1 () Float32 ((_ to_fp 8 24) #x3dcccccd))
(define-fun f1 () Float32 ((_ to_fp 8 24) #x3f800000))
(define-fun f1000 () Float32 ((_ to_fp 8 24) #x447a0000))
(declare-fun x () Float32)
(declare-fun y () Float32)
(assert (and (isFinite x) (fp.geq x f0)))
(assert (fp.lt f0_1 y f1))
(assert (fp.leq (fp.div RNE x f1000) y))
(define-fun res () Float32 (fp.div RNE x y))
(assert (not (fp.lt res f1000)))
(check-sat)
(exit)
