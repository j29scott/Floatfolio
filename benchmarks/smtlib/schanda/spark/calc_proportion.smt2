(set-info :smt-lib-version 2.6)
;;; Processed by pysmt to remove constant-real bitvector literals
(set-logic QF_FP)
(set-info :source |SPARK inspired floating point problems by Florian Schanda|)
(set-info :category "crafted")
(set-info :status unsat)
(define-fun isFinite ((f Float32)) Bool (or (fp.isNormal f) (fp.isSubnormal f) (fp.isZero f)))
(declare-fun a () Float32)
(declare-fun x () Float32)
(define-fun float__last () Float32 ((_ to_fp 8 24) #x7F7FFFFF))
(define-fun f0 () Float32 (_ +zero 8 24))
(define-fun f1 () Float32 ((_ to_fp 8 24) #x3f800000))
(define-fun f30 () Float32 ((_ to_fp 8 24) #x41f00000))
(assert (and (fp.geq a f0) (fp.leq a f30)))
(assert (and (fp.geq x f0) (fp.leq x f30)))
(assert (fp.gt x (fp.div RNE f30 float__last)))
(assert (fp.leq a x))
(define-fun res () Float32 (fp.div RNE a x))
(assert (not (and (fp.geq res f0) (fp.leq res f1))))
(check-sat)
(exit)
