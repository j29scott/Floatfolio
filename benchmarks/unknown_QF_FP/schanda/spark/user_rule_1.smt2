(set-info :smt-lib-version 2.6)
;;; Processed by pysmt to remove constant-real bitvector literals
(set-logic QF_FP)
(set-info :source |SPARK inspired floating point problems by Florian Schanda|)
(set-info :category "crafted")
(set-info :status unknown)
(define-fun is_finite ((f Float64)) Bool (or (fp.isZero f) (fp.isNormal f) (fp.isSubnormal f)))
(define-fun is_obvious ((f Float64)) Bool (or (fp.isZero f) (fp.isNormal f)))
(declare-fun a () Float64)
(declare-fun b () Float64)
(declare-fun c () Float64)
(define-fun d () Float64 ((_ to_fp 11 53) #x433fffffffffffff))
(assert (is_obvious a))
(assert (is_obvious b))
(assert (is_obvious c))
(assert (is_obvious d))
(assert (is_finite (fp.mul RNE a b)))
(assert (is_finite (fp.mul RNE c d)))
(assert (fp.leq c ((_ to_fp 11 53) #x41301b7000000000)))
(assert (fp.leq (fp.mul RNE a b) (fp.mul RNE c d)))
(assert (fp.gt c (_ +zero 11 53)))
(assert (not (fp.leq (fp.div RNE (fp.mul RNE a b) c) d)))
(check-sat)
(exit)
